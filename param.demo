#
# $Id: param.demo,v 3.26 92/03/24 22:32:22 woo Exp Locker: woo $
#
#
# Show some of the new parametric capabilities.
#
set parametric
set dummy t
set autoscale
set samples 160
set title ""
plot t,sin(t)/t title "t,sin(t)/t or sin(x)/x"
pause -1 "Hit return to continue"

plot sin(t)/t,t
pause -1 "Hit return to continue"

plot sin(t),cos(t)
pause -1 "Hit return to continue"

set xrange [-3:3]
set yrange [-3:3]
set title "Parametric Conic Sections"
plot -t,t,cos(t),cos(2*t),2*cos(t),sin(t),-cosh(t),sinh(t)
set title ""
pause -1 "Hit return to continue"

set xrange [-5:5]
set yrange [-5:5]
plot tan(t),t,t,tan(t)
pause -1 "Hit return to continue"

set trange [-3:3]
plot t,log(t),-t,log(t),sin(t),t**2
pause -1 "Hit return to continue"

set autoscale x
set yrange [-1.5:1.5]
set trange [0:10*pi]
plot sin(t)/t,cos(t)/t
pause -1 "Hit return to continue"

# undo what we've done above
set noparametric
set samples 160
set autoscale xy
set title ""
set offset 0,0,0,0
set key
