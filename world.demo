#
# $Id: world.demo,v 3.26 92/03/24 22:32:31 woo Exp Locker: woo $
#
set title "Gnuplot Correspondences"
set nokey
set noborder
set noyzeroaxis
set noxtics
set noytics
#
# plot world map and correspondent locations as a +
plot 'world.dat' with lines 3 4, 'world.cor' with points 1 2
set title ""
set key
set border
set yzeroaxis
set xtics
set ytics
pause -1 "Hit return to continue"
#
# plot a '3D version using spherical coordinate system' of the world.
set angles degrees
set title "3D version using spherical coordinate system"
set view 70,40,,2.0
set mapping spherical
set parametric
set samples 32
set isosamples 9
set urange [-pi/2:pi/2]
set vrange [0:2*pi]
splot 'world.dat' with lines 3 4, 'world.cor' with points 1 2, cos(u)*cos(v),cos(u)*sin(v),sin(u) with lines 5 6
pause -1 "Hit return to continue"
#
# plot a '3D version using cylindrical coordinate system' of the world.
set title "3D version using cylindrical coordinate system"
set view 70,40,,2.0
set mapping cylindrical
set parametric
set samples 32
set isosamples 9
set urange [-pi:pi]
set vrange [-90:90]
splot 'world.dat' with lines 3 4, 'world.cor' with points 1 2, cos(u),sin(u),v with lines 5 6
pause -1 "Hit return to continue"


#
# Clean up:
#
set noparametric
set mapping cartesian
set angles radians
set samples 100
set isosamples 10
set view 60,30,1,1
set xrange [-10:10]
set yrange [-10:10]
set zrange [-10:10]
set auto
set title "" 0,0
