#
# $Id: surface2.dem%v 3.38.2.139 1993/06/19 01:03:49 woo Exp woo $
#
set parametric
set isosamples 50,10
set hidden

set title "Parametric Sphere"
set urange [-pi/2:pi/2]
set vrange [0:2*pi]
set zrange [-1:1]
set ztics -1.0,0.25,1.0
set view 45,50,,2.5
splot cos(u)*cos(v),cos(u)*sin(v),sin(u)
pause -1 "Hit return to continue (1)"

set view ,,,0.8
replot
pause -1 "Hit return to continue (2)"

set view ,,,1.6
replot
pause -1 "Hit return to continue (3)"

set view ,,,1.0

set title "Parametric Torus"
set urange [0:2*pi]
set vrange [0:2*pi]
splot (1-0.2*cos(v))*cos(u),(1-0.2*cos(v))*sin(u),0.2*sin(v)
pause -1 "Hit return to continue (4)"


set title "Parametric Hexagon"
set urange [-1.3:1.3]
set vrange [0:2*pi]
set zrange [-0.6:0.6]
set ztics
splot cos(v)**3*cos(u)**3,sin(v)**3*cos(u)**3,sin(u)**3
pause -1 "Hit return to continue (5)"

set title "Parametric Helix"
set isosamples 100,20
set urange [0:10*pi]
set vrange [0:2*pi]
set zrange [-0.6:0.6]
splot (1-0.1*cos(v))*cos(u),(1-0.1*cos(v))*sin(u),0.1*(sin(v)+u/1.7-10)
pause -1 "Hit return to continue (6)"

set title "Parametric Shell"
set isosamples 40,20
set view 50,30,1.0
set urange [0:2*pi]
set vrange [0:2*pi]
set zrange [-3:1.5]
splot cos(u)*u*(1+cos(v)/2),sin(v)*u/2,sin(u)*u*(1+cos(v)/2)
pause -1 "Hit return to continue (7)"

set title "Interlocking Tori"

set urange [-pi:pi]
set vrange [-pi:pi]
set isosamples 50,20
splot cos(u)+.5*cos(u)*cos(v),sin(u)+.5*sin(u)*cos(v),.5*sin(v) with lines, 1+cos(u)+.5*cos(u)*cos(v),.5*sin(v),sin(u)+.5*sin(u)*cos(v) with lines
pause -1 "Hit return to continue (8)"

#
# Clean up:
#
set noparametric
set dummy x,y
set isosamples 10,10
set view 60,30,1,1
set urange [-5:5]
set vrange [-5:5]
set zrange [-10:10]
set auto
set title "" 0,0
set nohidden
