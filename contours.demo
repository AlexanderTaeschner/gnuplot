#
# $Id: contours.demo,v 3.26 92/03/24 22:32:18 woo Exp Locker: woo $
#
set samples 20
set isosamples 21
set xlabel "X axis" -5,-2
set ylabel "Y axis" 4,-1
set zlabel "Z axis"
set title "3D gnu plot demo - contour plot"
set contour
splot x*y
pause -1 "Hit return to continue (1)"
set cntrparam levels 20
set title "3D gnu plot demo - contour plot (more contours)"
replot
pause -1 "Hit return to continue (2)"
set cntrparam levels 40
set title "3D gnu plot demo - contour plot (and again more contours)"
replot
pause -1 "Hit return to continue (3)"
set cntrparam levels 10
set title "3D gnu plot demo - contour plot on base grid"
set contour base
splot x**2-y**2
pause -1 "Hit return to continue (4)"
set title "3D gnu plot demo - contour plot on surface"
set contour surface
replot
pause -1 "Hit return to continue (5)"
set title "3D gnu plot demo - contour plot on both"
set contour both
replot
pause -1 "Hit return to continue (6)"
set contour base
set title "3D gnu plot demo - 2 surfaces
splot x**2*y**3, x**3*y**2
pause -1 "Hit return to continue (7)"
set title "3D gnu plot demo - some more interesting contours"
splot x*y / (x**2 + y**2 + 0.1)
pause -1 "Hit return to continue (8)"
splot [x=-3:3] [y=-3:3] sin(x) * cos(y)
pause -1 "Hit return to continue (9)"
set zrange [-0.5:0.5]
replot
pause -1 "Hit return to continue (10)"
set samples 6
set isosamples 6
set cntrparam levels 5
set title "3D gnu plot demo - low resolution (6x6)"
replot
pause -1 "Hit return to continue (11)"
set cntrparam bspline
set title "3D gnu plot demo - low resolution (6x6) using bspline approx."
replot
pause -1 "Hit return to continue (12)"
set cntrparam order 8
set title "3D gnu plot demo - low resolution (6x6) raise bspline order."
replot
pause -1 "Hit return to continue (13)"
set cntrparam linear
set auto
set title "3D gnu plot demo - low resolution (6x6) using linear contours."
splot x*y
pause -1 "Hit return to continue (14)"
set cntrparam order 4
set cntrparam bspline
set title "3D gnu plot demo - low resolution (6x6) using bspline approx."
replot
pause -1 "Hit return to continue (15)"
set samples 25
set isosamples 26
set title "3D gnu plot demo - contour of Sinc function"
splot [-5:5.01] [-5:5.01] sin(sqrt(x**2+y**2)) / sqrt(x**2+y**2)
pause -1 "Hit return to continue (16)"
splot [-12:12.01] [-12:12.01] sin(sqrt(x**2+y**2)) / sqrt(x**2+y**2)
pause -1 "Hit return to continue (17)"
set cntrparam levels 10
set xrange [0:15]
set yrange [0:15]
set auto
set zrange [-0.6:0.6]
set data style lines
set title "3D gnu plot demo - contour of data grid plotting"
set parametric
splot "glass.dat"
pause -1 "Hit return to continue (18)"
set zrange [-1.2:1.2]
set noparametric
splot "glass.dat" using 1
pause -1 "Hit return to continue (19)"
set view 0,0,1
set nosurface
set title "3D gnu plot demo - 2D contour projection of last plot"
replot
pause -1 "Hit return to continue (20)"


#
# Clean up:
#
set surface
set nocontour
set cntrparam levels 5
set cntrparam linear
set samples 100
set isosamples 10
set view 60,30,1,1
set xrange [-10:10]
set yrange [-10:10]
set zrange [-10:10]
set auto
set title "" 0,0
set xlabel "" 0,0
set ylabel "" 0,0
set zlabel "" 0,0
