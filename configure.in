dnl Process this file with autoconf to produce a configure script.
dnl configure.in for gnuplot 3.7 on Unix.
dnl
dnl $Id: configure.in,v 1.83.2.7 2000/10/22 13:50:50 joze Exp $
dnl
AC_INIT(src/graphics.c)
AM_CONFIG_HEADER(config.h:config.hin)
AC_PREREQ(2.13)
AC_REVISION($Revision: 1.83.2.7 $)

VERSION="`cat $srcdir/VERSION`"
AM_INIT_AUTOMAKE(gnuplot, $VERSION)

dnl Additional command line options

dnl Use builtin readline or GNU readline
AC_ARG_WITH(readline,dnl
[  --with-readline         use the builtin minimal readline function (default)
  --without-readline      do not use the builtin readline function
  --with-readline=gnu     use the GNU readline library
  --with-readline=PATH            Specify the location of GNU readline],,
test -z "$with_readline" && with_readline=yes
)dnl

dnl If no PATH argument given, use the Unix plot library
dnl Else use either GNU libplot from plotutils,
dnl or ancient GNU graphics libplot
AC_ARG_WITH(plot,dnl
[  --with-plot             use the Unix plot library
  --with-plot=PATH                Specify the location of GNU libplot],,
test -z "$with_plot" && with_plot=no
)dnl

dnl The gif terminal with Tom Boutell's gd library
AC_ARG_WITH(gd,dnl
[  --with-gd               enable gif terminal with Tom Boutell's gd library
                          (requires GD library)
  --with-gd=PATH                  Specify the location of libgd],,
test -z "$with_gd" && with_gd=yes
)dnl

dnl The Portable Network Graphics (png) terminal
AC_ARG_WITH(png,dnl
[  --with-png              enable png terminal
                          (requires libpng and libz)
  --with-png=PATH                 Specify the location of libpng],,
test -z "$with_png" && with_png=yes
)dnl

dnl Use .gnuplot file in current directory
AC_ARG_WITH(cwdrc,dnl
[  --with-cwdrc            check current directory for .gnuplot file,
                          normally disabled for security reasons],,
test -z "$with_cwdrc" && with_cwdrc=no
)dnl

dnl Install the lasergnu printer script
AC_ARG_WITH(lasergnu,dnl
[  --with-lasergnu         install lasergnu printer script],
if test "$withval" = yes; then LASERGNU=lasergnu_install; \
else LASERGNU=lasergnu_noinstall; fi, LASERGNU=lasergnu_noinstall
)dnl

dnl The Linux console driver
AC_ARG_WITH(linux-vga,dnl
[  --with-linux-vga        use the Linux SVGA console driver
                          (requires /usr/lib/libvga)],,
test -z "$with_linux_vga" && with_linux_vga=no
)dnl

dnl Wether we want to build the lisp files
AC_ARG_WITH(lisp-files,dnl
[  --without-lisp-files    do not build emacs lisp files],,
test -z "$with_lisp_files" && with_lisp_files=yes
)dnl

dnl Wether we want to create the LaTeX tutorial
AC_ARG_WITH(tutorial,dnl
[  --without-tutorial      do not build LaTeX tutorial],,
test -z "$with_tutorial" && with_tutorial=yes
)dnl

dnl Gnuplot history, needs GNU readline
AC_ARG_ENABLE(history-file,dnl
[  --enable-history-file   use history file (requires GNU readline)],,
test -z "$enable_history_file" && enable_history_file=no
)dnl

dnl thin plate splines patch
AC_ARG_ENABLE(thin-splines,dnl
[  --enable-thin-splines   enable thin plate splines (EXPERIMENTAL)],,
test -z "$enable_thin_splines" && enable_thin_splines=no
)dnl

dnl Sort help/subtopic tables by row or column
AC_ARG_WITH(row-help,dnl
[  --with-row-help         format help and subtopic tables by row (default)
  --without-row-help      format help and subtopic tables by column],,
test -z "$with_row_help" && with_row_help=yes
)dnl

dnl IRIS terminal on IRIS4D series computer
AC_ARG_ENABLE(iris,dnl
[  --enable-iris           enable IRIS terminal (IRIS4D only)],,
test -z "$enable_iris" && enable_iris=no
)dnl

dnl MGR Window system
AC_ARG_ENABLE(mgr,dnl
[  --enable-mgr            enable MGR terminal],,
test -z "$enable_mgr" && enable_mgr=no
)dnl

dnl Redwood Graphics Interface Protocol
AC_ARG_ENABLE(rgip,dnl
[  --enable-rgip           enable Redwood Graphics Interface Protocol],,
test -z "$enable_rgip" && enable_rgip=no
)dnl

dnl use mouse for x11 interface
AC_ARG_ENABLE(mouse,dnl
[  --enable-mouse          enable mouse for the x11 terminal (EXPERIMENTAL)],,
test -z "$enable_mouse" && enable_mouse=no
)dnl

dnl pm3d
AC_ARG_ENABLE(pm3d,dnl
[  --enable-pm3d           enable pm3d (EXPERIMENTAL)],,
test -z "$enable_pm3d" && enable_pm3d=no
)dnl

dnl new Enhanced Metafile Format driver
AC_ARG_ENABLE(emf,dnl
[  --enable-emf            enable Enhanced Metafile Format driver (EXPERIMENTAL)],,
test -z "$enable_emf" && enable_emf=no
)dnl

dnl new epslatex driver
AC_ARG_ENABLE(epslatex,dnl
[  --enable-epslatex       enable epslatex driver (EXPERIMENTAL)],,
test -z "$enable_epslatex" && enable_epslatex=no
)dnl

dnl new ggi driver
AC_ARG_ENABLE(ggi,dnl
[  --enable-ggi            enable ggi driver (EXPERIMENTAL)],,
test -z "$enable_ggi" && enable_ggi=no
)dnl

dnl use a different x11 driver name ?
AC_ARG_WITH(x11-driver,
[  --with-x11-driver       specify an alternative X11 driver name (gnuplot_x11)],
GNUPLOT_X11="$withval",
GNUPLOT_X11=gnuplot_x11
)dnl

dnl configure.in body

dnl Compiler characteristics
dnl Check for ANSI C prototypes, the const and inline keywords,
dnl and ANSI style stringification
AC_PROG_CC
AM_C_PROTOTYPES
AC_PROG_CPP
AC_C_CONST
AC_C_INLINE
AC_C_STRINGIZE

dnl X Window System files.
AC_SUBST(LIBRARIES_FOR_X)
AC_PATH_XTRA

dnl Needed for LynxOS until AC_PATH_XTRA is fixed
if test "$ac_cv_func_gethostbyname" = no; then
  if test "$ac_cv_lib_nsl_gethostbyname" = no; then
    AC_CHECK_LIB(bsd, gethostbyname, X_EXTRA_LIBS="$X_EXTRA_LIBS -lbsd")
  fi
fi

if test "$no_x" != yes; then
  CPPFLAGS="$CPPFLAGS $X_CFLAGS"
  LIBRARIES_FOR_X="$X_LIBS -lX11 $X_EXTRA_LIBS"
  AC_DEFINE(X11, 1,
            [ Define if you are using the X11 window system. ])

  AC_DEFINE_UNQUOTED(GNUPLOT_X11, "$GNUPLOT_X11",
            [ Define the default x11 driver name. ])
  AM_CONDITIONAL(ALT_X11_DRIVER, test x$GNUPLOT_X11 != xgnuplot_x11)
fi

AC_SUBST(GNUPLOT_X11)

dnl Operating systems.
dnl FIXME AC_DEFINE(ISC22)
dnl FIXME AC_DEFINE(KSR)
dnl Check for MSDOS and djgpp, NeXT, Apple MacOsX (NeXT like), BeOS
GP_MSDOS
GP_NEXT
GP_APPLE
GP_BEOS
AM_CONDITIONAL(BUILD_SRC_BEOS_SUBDIR, test x$build_beos_subdir = xyes)

dnl Apparently, -lNeXT_s is needed on NeXT
dnl _instead_ of -lm ...
AC_CHECK_FUNC(sin)
if test "$ac_cv_func_sin" = no ; then
  AC_CHECK_LIB(m,sin)
fi
dnl this has to be after -lsys_s on NeXT
dnl AC_CHECK_LIB(m,sin)

dnl Header files. ANSI first
dnl We prefer that the absense of a macro is the norm, so in syscfg.h
dnl configure's HAVE_XXXX defines are translated into NO_XXXX for ANSI
dnl headers and functions
AC_HEADER_STDC

dnl Header files
dnl ANSI/ISO C, POSIX, others
AC_CHECK_HEADERS(errno.h float.h limits.h locale.h math.h stdlib.h string.h \
  time.h sys/time.h sys/types.h \
  sys/bsdtypes.h sys/ioctl.h sys/param.h sys/select.h sys/socket.h \
  sys/stat.h sys/systeminfo.h sys/timeb.h sys/utsname.h \
  libc.h malloc.h poll.h sgtty.h termios.h values.h
)

test "$ac_cv_header_sys_stat_h" = yes && AC_HEADER_STAT

dnl check if unistd actually declares anything. On NeXT 3.2 unistd is
dnl conditionalized for _POSIX_SOURCE
AC_MSG_CHECKING(for unistd.h)
AC_EGREP_HEADER(execv, unistd.h, AC_DEFINE(HAVE_UNISTD_H) AC_MSG_RESULT(yes), AC_MSG_RESULT(no))

dnl check if errno.h header declares errno
AC_MSG_CHECKING(if errno variable is declared)
AC_TRY_COMPILE(
[#include <stdio.h>
#ifdef HAVE_ERRNO_H
#include <errno.h>
#endif],
  errno=0,AC_MSG_RESULT(yes),
  AC_DEFINE(EXTERN_ERRNO, 1,
            [ Define if <errno.h> declares errno. ])
  AC_MSG_RESULT(no)
)dnl


dnl Types.
AC_TYPE_SIZE_T
AC_MSG_CHECKING(for time_t in time.h)
AC_EGREP_HEADER(time_t,time.h,
  [ ac_cv_type_time_t=yes; AC_MSG_RESULT(yes) ],
  AC_MSG_RESULT(no)
  AC_CHECK_TYPE(time_t, long))
AC_TYPE_SIGNAL
 

dnl Functions. Standard first, then others

dnl ANSI/ISO and their predecessors
dnl sunos 4 has on_exit() in place of atexit()
dnl gamma is called lgamma on apollos and linux
dnl we prefer lgamma over gamma, see specfun.c
dnl math lib is already available, see operating systems part
AC_CHECK_FUNCS(atexit memcpy memmove memset \
  on_exit bcopy bzero \
  setvbuf strerror strchr strrchr strstr \
  index rindex \
  erf erfc gamma lgamma \
  getcwd poll pclose popen select sleep snprintf stpcpy \
  strcspn strdup strcasecmp stricmp strncasecmp strnicmp \
  sysinfo tcgetattr vfprintf doprnt
)

dnl check, if we have sigsetjmp and siglongjmp.
dnl a trivial AC_CHECK_FUNCS(sigsetjmp) won't do
dnl because sigsetjmp() might be a macro declared
dnl in <setjmp.h>. (joze)
AC_MSG_CHECKING(for sigsetjmp)
AC_TRY_LINK([#include <setjmp.h>], [jmp_buf env; sigsetjmp(env, 1);],
  [AC_MSG_RESULT(yes)
   AC_DEFINE(HAVE_SIGSETJMP, 1,
             [ Define if we have sigsetjmp(). ])],
  AC_MSG_RESULT(no)
)

if test "$ac_cv_func_pclose" = yes && test "$ac_cv_func_popen" = yes ; then
  AC_DEFINE(PIPES, 1,
            [ Define if you do have the popen and pclose functions. ])
fi

dnl Argument types of select()
AC_FUNC_SELECT

dnl On SVR3.
dnl FIXME AC_DEFINE(CRIPPLED_SELECT)

dnl check for installed linux vgalib
if test "$with_linux_vga" = yes; then
  AC_MSG_CHECKING(for linux vga library)
  AC_CHECK_LIB(vga, vga_init,
    [AC_DEFINE(LINUXVGA, 1,
               [ Define if this is a Linux system with SuperVGA library. ])
     LINUXSUID='chown root $(bindir)/gnuplot; chmod u+s $(bindir)/gnuplot'
     TERMLIBS="$TERMLIBS -lvga"],
    with_linux_vga=no)
fi

dnl check for installed linux vgagl lib if
dnl both pm3d and linux_vga are defined.
if test "$with_linux_vga" = yes -a "$enable_pm3d" = yes; then
  dnl save $LIBS
  olibs=$LIBS
  dnl make shure LIBS contains -lvga
  LIBS="${TERMLIBS} ${LIBS}"
  AC_MSG_CHECKING(for the vgagl library)
  AC_CHECK_LIB(vgagl, gl_hline,
    [
      dnl add -lvgagl to LIBS
      LIBS="-lvgagl ${TERMLIBS} ${LIBS}"
      AC_CHECK_HEADERS(vgagl.h,
	[
	  AC_DEFINE(VGAGL, 1, [ Define if the vgagl libray is present. ])
	  dnl yup, we've the libvgagl.
	  dnl check now, if lib3dkit is found.
	  AC_CHECK_LIB(3dkit, gl_striangle,
	    [
	      AC_CHECK_HEADERS(3dkit.h,
		[
		  dnl success! If we're here, all checks for
		  dnl the vgagl terminal driver succeeded.
		  AC_DEFINE(THREEDKIT, 1, [ Define if the 3dkit libray is present. ])
		  AC_DEFINE(EXTENDED_COLOR_SPECS, 1,
		     [ Define if color information should be passed for each vertex. ])
		  VGAGLLIB="-l3dkit -lvgagl"
		  with_vgagl=yes
		],dnl <-- 3dkit.h found
		[
		  with_vgagl=no
		  AC_MSG_RESULT([unable to find 3dkit.h])
		]dnl <-- 3dkit.h not found
	      )
	    ],dnl <-- 3dkit is present
	    [
	      with_vgagl=no
	      AC_MSG_RESULT([unable to find lib3dkit])
	    ]dnl <-- 3dkit is not present
	  )
	],dnl <-- vgagl.h is present
        [
	  with_vgagl=no
	  AC_MSG_RESULT([unable to find vgagl.h])
	]dnl <-- vgagl.h is not present
      )
    ],dnl <-- libvgagl was found
    [
      with_vgagl=no
      AC_MSG_RESULT([unable to find libvgagl])
    ]dnl <-- libvgagl was not found
  )
  dnl restore LIBS
  LIBS=$olibs
fi

TERMLIBS="$VGAGLLIB $TERMLIBS"

dnl GNU readline and the required terminal library
if test "$with_readline" != no; then
  if test "$with_readline" != yes; then
    dnl check for terminal library
    dnl this is a very cool solution from octave's configure.in
    gp_tcap=""
    for termlib in ncurses curses termcap terminfo termlib; do
      AC_CHECK_LIB(${termlib}, tputs, [gp_tcap="$gp_tcap -l$termlib"])
      case "$gp_tcap" in
        *-l${termlib}*)
          break
        ;;
      esac
    done

    if test "$with_readline" = gnu; then
      AC_CHECK_LIB(readline, readline,
       TERMLIBS="$TERMLIBS -lreadline $gp_tcap",, ${gp_tcap})
    else
      GP_PATH_LIB(readline,readline,[$with_readline],[${gp_tcap}])
      if test "$ac_cv_lib_readline_readline" = yes; then
        TERMLIBS="$TERMLIBS $gp_tcap"
      fi
    fi

    if test "$ac_cv_lib_readline_readline" = no; then
      AC_MSG_WARN([GNU readline not found - falling back to builtin readline])
      with_readline=yes
    else
      AC_DEFINE(HAVE_LIBREADLINE, 1,
                [ Define if you are using the GNU readline library. ])
      GP_PATH_HEADER(readline/readline.h,[$with_readline],,
        AC_MSG_WARN([found GNU readline library but not readline.h])
        AC_MSG_WARN([please add path to readline.h to CPPFLAGS in Makefile])
      )
      GP_PATH_HEADER(readline/history.h,[$with_readline],,
        AC_MSG_WARN([found GNU readline library but not history.h])
        AC_MSG_WARN([please add path to history.h to CPPFLAGS in Makefile])
      )

      dnl check if history feature is desired
      if test "$enable_history_file" = yes; then
        GP_PATH_HEADER(readline/tilde.h,[$with_readline],
          AC_DEFINE(GNUPLOT_HISTORY, 1,
                    [ Define if you want to use a gnuplot history file with GNU readline. ]))
      fi

    fi # ! ac_cv_lib_readline_readline = no

  fi # ! with_readline != yes

  if test "$with_readline" = yes; then
    AC_DEFINE(READLINE, 1,
              [ Define if you want to use the included readline function. ])
  fi

fi
dnl end readline

dnl libplot
if test "$with_plot" != no; then
  if test "$with_plot" = yes; then
    dnl Unix plot library
    AC_CHECK_LIB(plot,openpl,
     AC_DEFINE(UNIXPLOT, 1,
               [ Define if you want to use the standard Unix plot library. ])
     TERMLIBS="$TERMLIBS -lplot",
     with_plot=no)
  else # require GNU plotutils 2.2 or better
    TERMXLIBS="$X_LIBS -lXaw -lXmu -lXt -lXext -lX11"
    GP_PATH_LIB(plot,pl_textangle,[$with_plot],[$TERMXLIBS])
    if test "$ac_cv_lib_plot_pl_textangle" = no; then
      AC_MSG_WARN([GNU plotutils v2.2 or newer required -- disabling gnugraph terminal])
      with_plot=no
    fi # GNU plotutils v2.2

    if test "$with_plot" != no; then
      AC_DEFINE(GNUGRAPH, 1,
                [ Define if you want to use the GNU version of the Unix plot library. ])
    fi

  fi # ! Unix plotlib

fi
dnl end libplot

dnl check for Tom Boutell's gd library
if test "$with_gd" != no; then
  GP_PATH_LIB(gd,gdImageCreate,[$with_gd])
  if test "$ac_cv_lib_gd_gdImageCreate" = yes; then
    GP_PATH_HEADER(gd.h,[$with_gd],
      dnl ACTION-IF-FOUND
      AC_DEFINE(HAVE_LIBGD, 1,
                [ Define if you have Thomas Boutell's gd library. ]),
      dnl ACTION-IF-NOT-FOUND
      AC_MSG_WARN([found gd library but not gd.h])
      AC_MSG_WARN([please add path to gd.h to CPPFLAGS in Makefile]))
  else
    AC_MSG_RESULT([- see term/gif.trm for download details])
  fi
fi
dnl end gd

dnl check for png and z library
if test "$with_png" != no; then
  GP_PATH_LIB(png,png_info_init,[$with_png],[-lz])
  if test "$ac_cv_lib_png_png_info_init" = yes; then
    TERMLIBS="$TERMLIBS -lz"
    GP_PATH_HEADER(png.h,[$with_png],,
     AC_MSG_WARN([found png library but not png.h])
     AC_MSG_WARN([please add path to png.h to CPPFLAGS in Makefile])
    )
    GP_PATH_HEADER(zlib.h,[$with_png],,
     AC_MSG_WARN([found z library but not zlib.h])
     AC_MSG_WARN([please add path to zlib.h to CPPFLAGS in Makefile])
    )
    if test "$ac_cv_header_png_h" = yes; then
      AC_EGREP_CPP([You lose], [#include "png.h"
#if PNG_LIBPNG_VER < 89
You lose.
#endif
],
       AC_MSG_WARN([- libpng too old - not including png driver])
       ac_cv_header_png_h=no)
    else
      AC_MSG_WARN([- cannot check png version - compilation may fail])
    fi

  if test "$ac_cv_header_png_h" = yes; then
    AC_DEFINE(HAVE_LIBPNG, 1,
              [ Define if you have the png library. ])
  fi

  else
    AC_MSG_RESULT([- see term/png.trm for download details])
  fi
fi
dnl end png and z

dnl How do we detect the Ultrix X libraries?
dnl Or is it really a server (run-time) problem?
dnl FIXME AC_DEFINE(ULTRIX_KLUDGE)

dnl Removed enable options for these
dnl Just autodetect

dnl SCO cgi
AC_EGREP_CPP(yes,
[#ifdef SCO
yes
#endif
],AC_CHECK_LIB(ccgi, v_opnwk,
    AC_DEFINE(CGI, 1,
              [ Define if you want to use the CGI terminal under SCO. ])
              TERMLIBS="$TERMLIBS -lccgi"))

dnl Sun sunview
AC_EGREP_CPP(yes,
[#ifdef sun
yes
#endif
], AC_CHECK_LIB(suntool, window_create,
    AC_CHECK_HEADER(suntool/sunview.h,
    AC_DEFINE(SUN, 1,
              [ Define if you want to use the sunview terminal (sun). ])
    TERMLIBS="$TERMLIBS -lsuntool -lsunwindow -lpixrect"),,
    [-lsunwindow -lpixrect]))

dnl Process with and enable options

if test "$with_cdwrc" = no; then
  AC_DEFINE(NOCWDRC, 1,
            [ Define if you do not want to read .gnuplot in the current directory. ])
fi

if test "$enable_thin_splines" = yes; then
  AC_DEFINE(THIN_PLATE_SPLINES_GRID, 1,
            [ Define to use thin plate spines. EXPERIMENTAL ])
fi

if test "$with_row_help" = no; then
  AC_DEFINE(COLUMN_HELP, 1,
            [ Define if you want online help and subtopic tables sorted by column. ])
fi

if test "$enable_iris" = yes; then
  AC_CHECK_LIB(gl_s,winopen,
   AC_DEFINE(IRIS, 1,
             [ Define if you want to use the IRIS terminal on IRIS4D series computers. ])
   TERMLIBS="$TERMLIBS -lgl_s",
   enable_iris=no)
fi

if test "$enable_mgr" = yes; then
  AC_CHECK_LIB(mgr,m_getinfo,
   AC_DEFINE(MGR, 1,
             [ Define if you want to use the MGR Window system. ])
   TERMLIBS="$TERMLIBS -lmgr -lpixrect",
   enable_mgr=no,[-lpixrect])
fi

if test "$enable_rgip" = yes; then
  AC_DEFINE(RGIP, 1,
            [ Define if you want to use the Redwood Graphics Interface Protocol. ])
fi

if test "$enable_mouse" = yes; then
  AC_DEFINE(USE_MOUSE, 1,
            [ Define if you want to have mouse support for the x11 terminal. ])
fi

if test "$enable_pm3d" = yes; then
  AC_DEFINE(PM3D, 1,
            [ Define if you want to have Petr Mikulik's fancy pm3d. ])
fi

if test "$enable_emf" = yes; then
  AC_DEFINE(USE_EMF_DRIVER, 1,
            [ Define if you want to use the experimental Enhanced Metafile Format driver. ])
fi

if test "$enable_epslatex" = yes; then
  AC_DEFINE(USE_EPSLATEX_DRIVER, 1,
            [ Define if you want to use the experimental epslatex driver. ])
fi

dnl check for ggi lib
GGI_SUPPORT=no
if test "$enable_ggi" = yes; then
    AC_CHECK_LIB(ggi, ggiInit,
	[dnl action if lib is ok
	    TERMLIBS="$TERMLIBS -lggi"
	    AC_DEFINE(USE_GGI_DRIVER, 1,
		[ Define if you want to use the experimental ggi driver. ])
	    GGI_SUPPORT=yes
	],
	[dnl action if lib was not found
	    AC_MSG_RESULT(no)
	]
    )
fi


dnl Substitute variables
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(LASERGNU)
AC_SUBST(LINUXSUID)
AC_SUBST(TERMLIBS)
AC_SUBST(TERMXLIBS)

dnl Build gnuplot-mode in lisp only
dnl if a suitable emacs is found
if test "$with_lisp_files" = yes; then
  AC_CHECK_PROGS(EMACS, emacs xemacs, no)
else
  EMACS=no
fi
if test "$EMACS" != no; then
  config_lisp=lisp
else
  config_lisp=
fi
AC_CONFIG_SUBDIRS($config_lisp)
AM_CONDITIONAL(BUILD_LISP_SUBDIR, test x$config_lisp = xlisp)

dnl Build LaTeX tutorial only if
dnl latex/2e is found
if test "$with_tutorial" = yes; then
  AC_CHECK_PROGS(LATEX, latex latex2e, no)
else
  LATEX=no
fi
if test "$LATEX" = no; then
  TUTORIAL=notutorial
else
  TUTORIAL=tutorial
fi
AC_SUBST(TUTORIAL)

dnl for pdf format docs
AC_CHECK_PROGS(PDFLATEX, pdflatex, no)

dnl Write Makefiles and configuration header
AC_OUTPUT([Makefile
           config/Makefile
           demo/Makefile
           docs/Makefile
           m4/Makefile
           man/Makefile
           src/Makefile
           term/Makefile
           tutorial/Makefile], \
          [test -z "$CONFIG_HEADERS" || echo timestamp >stamp-h
          ])

dnl Report configuration
eval gp_datadir=$datadir
test $gp_datadir = NONE/share && gp_datadir=/usr/local/share
AC_MSG_RESULT([
** Configuration summary for $PACKAGE $VERSION:

  Where is the help file?           $gp_datadir/gnuplot.gih
])
test "$ac_cv_header_gd_h" = yes && \
  AC_MSG_RESULT([  Enable generation of GIF files])

test "$ac_cv_header_png_h" = yes && \
  AC_MSG_RESULT([  Enable generation of PNG files])

test "$with_cwdrc" = yes && \
  AC_MSG_RESULT([  Check current directory for .gnuplot file])

if test "$with_readline" != no; then
  if test "$with_readline" != yes; then
    AC_MSG_RESULT([  Use GNU readline library])
      test -n "$gp_tcap" && AC_MSG_RESULT([    with ${gp_tcap}])
      test "$enable_history_file" = yes && \
        AC_MSG_RESULT([    enable history file])
  else
    AC_MSG_RESULT([  Use builtin minimal readline])
  fi
fi

if test "$with_plot" = yes; then
  AC_MSG_RESULT([  Use the Unix plot library])
elif test "$with_plot" != no; then
  AC_MSG_RESULT([  Use the GNU plot library])
fi

test "$with_lasergnu" = yes && \
  AC_MSG_RESULT([  Install lasergnu printer script])

if test "$with_linux_vga" = yes; then
  AC_MSG_RESULT([  Use the Linux SVGA console driver])
  AC_MSG_RESULT([    SECURITY NOTICE: SVGAlib requires that])
  AC_MSG_RESULT([    gnuplot is installed suid root!])
fi

if test "$with_vgagl" = yes; then
  AC_MSG_RESULT([  enable vgagl terminal with mouse and pm3d support])
fi

test "$enable_thin_splines" = yes && \
  AC_MSG_RESULT([  Enable thin plate splines (EXPERIMENTAL)])

if test "$with_row_help" = yes; then
  AC_MSG_RESULT([  Sort help/subtopic tables by row])
else
  AC_MSG_RESULT([  Sort help/subtopic tables by column])
fi

test "$enable_emf" = yes && \
  AC_MSG_RESULT([  Enable Enhanced Metafile Format terminal (EXPERIMENTAL)])

test "$enable_epslatex" = yes && \
  AC_MSG_RESULT([  Enable epslatex terminal (EXPERIMENTAL)])

test "$GGI_SUPPORT" = yes && \
  AC_MSG_RESULT([  Enable ggi terminal (EXPERIMENTAL)])

test "$enable_iris" = yes && \
  AC_MSG_RESULT([  Enable IRIS terminal])

test "$enable_mgr" = yes && \
  AC_MSG_RESULT([  Enable MGR terminal])

test "$enable_rgip" = yes && \
  AC_MSG_RESULT([  Enable RGIP terminal])

test "$EMACS" != no && \
  AC_MSG_RESULT([  Build gnuplot-mode for X/Emacs])

test "$LATEX" != no && \
  AC_MSG_RESULT([  Build LaTeX tutorial])

if test "$no_x" != yes; then
  AC_MSG_RESULT([  Use the X Window System ])
  if test "$GNUPLOT_X11" != gnuplot_x11; then
    AC_MSG_RESULT([  X11 driver name: $GNUPLOT_X11])
  fi
  if test "$enable_mouse" = yes; then
    AC_MSG_RESULT([  Enable MOUSE for X11 (EXPERIMENTAL)])
  fi
fi

if test "$enable_pm3d" = yes; then
  AC_MSG_RESULT([  Enable pm3d (EXPERIMENTAL)])
fi
dnl end config report

dnl end configure.in
