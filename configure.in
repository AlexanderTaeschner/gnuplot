dnl -*-Mode: M4 -*-
dnl Process this file with autoconf to produce a configure script.
dnl configure.in for gnuplot 3.5 (pre-3.6) on Unix.
dnl
dnl Where possible, please adopt a convention that the absense of
dnl a macro is the usual case : this saves having to update
dnl countless makefiles when new tests are added. By usual case
dnl I guess ansi compiler on posix system or something like that
dnl
dnl $Id: configure.in,v 1.90 1998/06/18 14:52:55 ddenholm Exp $
dnl
AC_PREREQ(2.1)
AC_REVISION($Revision: 1.90 $)
AC_INIT(graphics.c)
VERSION=`sed -n 's/.*patchlevel.*"\(.*\)".*/\1/p' $srcdir/version.c|tr -d ' '`
AM_INIT_AUTOMAKE(gnuplot, $VERSION)
AC_CONFIG_HEADER(config.h:config.hin)


dnl Programs. Other checks have already been done by AM_INIT_AUTOMAKE
AC_PROG_CC


dnl Optional features.

dnl Do not use the included readline function
AC_ARG_WITH(readline,dnl
[  --without-readline      do not use the included readline function],,
  AC_DEFINE(READLINE)
)dnl

dnl The GNU readline library
AC_ARG_WITH(gnu-readline,dnl
[  --with-gnu-readline     use the GNU readline version
  --with-gnu-readline=PATH        Specify the location of libreadline],,
test -z "$with_gnu_readline" && with_gnu_readline=no
)dnl

dnl The gif terminal with Tom Boutell's gd library
AC_ARG_WITH(gd,dnl
[  --with-gd               enable gif terminal with Tom Boutell's gd library
                          (requires GD library)
  --with-gd=PATH                  Specify the location of libgd],,
test -z "$with_gd" && with_gd=yes
)dnl

dnl The Portable Network Graphics (png) terminal
AC_ARG_WITH(png,dnl
[  --with-png              enable png terminal
                          (requires libpng and libz)
  --with-png=PATH                 Specify the location of libpng],,
test -z "$with_png" && with_png=yes
)dnl

dnl Use .gnuplot file in current directory
AC_ARG_WITH(cwdrc,dnl
[  --with-cwdrc            check current directory for .gnuplot file,
                          normally disabled for security reasons],
test "$withval" = no && AC_DEFINE(NOCWDRC), AC_DEFINE(NOCWDRC)
)dnl

dnl Install the lasergnu printer script
AC_ARG_WITH(lasergnu,dnl
[  --with-lasergnu         install lasergnu printer script],
if test "$withval" = yes; then LASERGNU=lasergnu_install; \
else LASERGNU=lasergnu_noinstall; fi, LASERGNU=lasergnu_noinstall
)dnl

dnl The Linux console driver
AC_ARG_WITH(linux-vga,dnl
[  --without-linux-vga     do not use the Linux console driver
                          (requires Linux SVGAlib /usr/lib/libvga)],,
test -z "$with_linux_vga" && with_linux_vga=yes
)dnl

dnl configure.in body

dnl Compiler characteristics

dnl figure out if can handle ANSI style prototypes
dnl with gcc we're well off, but other compilers might require additional
dnl options
AM_C_PROTOTYPES
AC_C_CONST
AC_C_INLINE

dnl this shell variable is set to no if above failed to set
dnl compiler to ansi mode (see aclocal.m4)
if test "$am_cv_prog_cc_stdc" != no; then
  AC_DEFINE(ANSI_C)
fi

dnl check for ANSI style stringification
gp_PROG_CPP_STRINGIFY

AC_MSG_CHECKING(if compiler handles warn and error directives correctly)
dnl this used to test #warning, but that is not currently used
AC_TRY_CPP([#if 0
#error "error"
#endif],ERRORFIX= AC_MSG_RESULT(yes),ERRORFIX=errorfix AC_MSG_RESULT(no))
AC_SUBST(ERRORFIX)


dnl X Window System files.
AC_SUBST(X_INCLUDES)
AC_SUBST(X_LIBRARIES)
AC_SUBST(GNUPLOT_X11)
AC_PATH_XTRA
if test "$no_x" = yes; then
  X_INCLUDES= X_LIBRARIES= GNUPLOT_X11=
else
  if test -n "$x_includes"; then
    X_INCLUDES="-I$x_includes"
  fi
  if test -n "$x_libraries"; then
    X_LIBRARIES="-L$x_libraries -lX11"
  else
    X_LIBRARIES="-lX11"
  fi
  CPPFLAGS="$CPPFLAGS $X_CFLAGS"
  X_LIBRARIES="$X_PRE_LIBS $X_LIBRARIES $X_LIBS $X_EXTRA_LIBS"
  GNUPLOT_X11=gnuplot_x11
fi


dnl Operating systems.
dnl FIXME AC_DEFINE(ISC22)
dnl FIXME AC_DEFINE(KSR)
dnl Check for MSDOS and djgpp, NeXT
gp_MSDOS
gp_NEXT

dnl this has to be after -lsys_s on NeXT
LIBS="$LIBS -lm"


dnl Header files. ANSI first
dnl We prefer that the absense of a macro is the norm, so we
dnl define NO_WHATEVER if an ansi header/fn is missing, but HAVE_SOMETHING
dnl if it is a non-ansi header/fn we would like to use if available

dnl check for errno.h header and if it declares errno
AC_CHECK_HEADER(errno.h, , AC_DEFINE(NO_ERRNO_H))
AC_MSG_CHECKING(if errno variable is declared)
AC_TRY_COMPILE(
[#include <stdio.h>
#ifndef NO_ERRNO_H
#include <errno.h>
#endif],
 errno=0,AC_MSG_RESULT(yes),AC_DEFINE(EXTERN_ERRNO) AC_MSG_RESULT(no))
dnl
AC_CHECK_HEADER(float.h, ,AC_DEFINE(NO_FLOAT_H))
AC_CHECK_HEADER(limits.h, ,AC_DEFINE(NO_LIMITS_H) AC_CHECK_HEADERS(values.h))
AC_CHECK_HEADER(locale.h, ,AC_DEFINE(NO_LOCALE_H))
AC_CHECK_HEADER(math.h, ,AC_DEFINE(NO_MATH_H))
AC_CHECK_HEADER(stdlib.h, ,AC_DEFINE(NO_STDLIB_H))
AC_CHECK_HEADER(string.h, ,AC_DEFINE(NO_STRING_H))
AC_CHECK_HEADER(time.h, ,AC_DEFINE(NO_TIME_H) AC_CHECK_TYPE(time_t, long))
dnl
AC_CHECK_HEADER(sys/time.h, , AC_DEFINE(NO_SYS_TIME_H))
AC_CHECK_HEADER(sys/types.h, , AC_DEFINE(NO_SYS_TYPES_H))
AC_CHECK_HEADER(sys/stat.h, AC_HEADER_STAT, AC_DEFINE(NO_SYS_STAT_H))
AC_CHECK_HEADERS(sys/bsdtypes.h sys/select.h sys/socket.h sys/systeminfo.h \
  sys/utsname.h libc.h malloc.h sgtty.h termios.h)

dnl check if unistd actually declares anything. On NeXT 3.2 unistd is
dnl conditionalized for _POSIX_SOURCE
AC_MSG_CHECKING(for unistd.h)
AC_EGREP_HEADER(execv, unistd.h, AC_DEFINE(HAVE_UNISTD_H) AC_MSG_RESULT(yes), AC_MSG_RESULT(no))


dnl Types.
AC_TYPE_SIZE_T
AC_TYPE_SIGNAL
 

dnl Functions. ANSI first, then others

dnl sunos 4 has on_exit() in place of atexit()
AC_CHECK_FUNC(atexit, ,AC_DEFINE(NO_ATEXIT) AC_CHECK_FUNCS(on_exit))
AC_CHECK_FUNC(memcpy, ,AC_DEFINE(NO_MEMCPY) AC_CHECK_FUNCS(bcopy))
AC_CHECK_FUNC(memset, ,AC_DEFINE(NO_MEMSET) AC_CHECK_FUNCS(bzero))
AC_CHECK_FUNC(setvbuf, ,AC_DEFINE(NO_SETVBUF))
AC_CHECK_FUNC(strerror, ,AC_DEFINE(NO_STRERROR))
AC_CHECK_FUNC(strchr, ,AC_DEFINE(NO_STRCHR) AC_CHECK_FUNCS(index))
AC_CHECK_FUNC(strrchr, ,AC_DEFINE(NO_STRRCHR) AC_CHECK_FUNCS(rindex))
AC_CHECK_FUNC(strstr, ,AC_DEFINE(NO_STRSTR))

dnl gamma is called lgamma on apollos and linux
dnl we prefer lgamma over gamma, see specfun.c
dnl math lib is already available, see operating systems part
AC_CHECK_FUNCS(erf gamma lgamma getcwd pclose popen sleep snprintf \
  strncasecmp strnicmp sysinfo tcgetattr)
if test "$ac_cv_func_pclose" = yes && test "$ac_cv_func_popen" = yes ; then
  AC_DEFINE(PIPES)
fi

dnl arg types for select
gp_FIND_SELECT_ARGTYPES

dnl On SVR3.
dnl FIXME AC_DEFINE(CRIPPLED_SELECT)


dnl Cached variables - required for the library checks below
if test ."$gp_cv_prog_make_cppflags" != "."; then
  CPPFLAGS="$gp_cv_prog_make_cppflags"
fi
if test ."$gp_cv_prog_make_termlibs" != "."; then
dnl Temporarily change the quoting character because autoconf
dnl is using brackets internally.
  changequote(,)dnl
dnl There is a space and a tab between the square brackets.
  TERMLIBS="`echo $gp_cv_prog_make_termlibs | sed 's/-l[^ 	]*//g'`"
  changequote([,])dnl
fi

dnl check for installed linux vgalib
if test "$with_linux_vga" = yes; then
  AC_MSG_CHECKING(for linux vga library)
  AC_CHECK_LIB(vga, vga_init,
    AC_DEFINE(LINUXVGA)
    LINUXSUID='chown root $(bindir)/gnuplot; chmod u+s $(bindir)/gnuplot'
    TERMLIBS="$TERMLIBS -lvga",with_linux_vga=no)
fi

dnl This will work only with gnu readline installed in a standard library path
dnl However, if libreadline.a is not installed in a standard path, then
dnl all subsequent library tests will fail because we will try to link with
dnl -lreadline.  This isn't acceptable, so we do check for the existence of
dnl readline in a standard path.
dnl
dnl most unices need termcap with readline, but not Debian linux, which
dnl does not even supply it, or so I am told

if test "$with_gnu_readline" = yes; then
    AC_CHECK_LIB(termcap, tputs, gp_tcap="-ltermcap", gp_tcap="")
    AC_CHECK_LIB(readline, readline, 
      LIBS="$LIBS -lreadline $gp_tcap",
   AC_MSG_ERROR([Can't find -lreadline in a standard path -- specify its location using --with-gnu-readline=/path/to/libreadline.a]),
	  $gp_tcap 
    )dnl readline
    AC_DEFINE(GNU_READLINE)

  elif test "$with_gnu_readline" != no; then
    if test ! -f $with_gnu_readline ; then
      AC_MSG_ERROR([No such file $with_gnu_readline])
    fi

    AC_CHECK_LIB(termcap, tputs, gp_tcap=" -ltermcap", gp_tcap="")
    LIBS="$LIBS $with_gnu_readline $gp_tcap"
    AC_DEFINE(GNU_READLINE)
fi
dnl end readline

dnl check for Tom Boutell's gd library
if test "$with_gd" != no; then
  gp_CHECK_LIB_PATH(gd, gdImageCreate)
  if test "$ac_cv_lib_gd_gdImageCreate" = yes; then
    gp_CHECK_HEADER(gd.h,,
     AC_MSG_WARN([please add path to `echo $ac_safe|sed 's/\_/\./'` to INCLUDES in Makefile])
    )dnl gp_CHECK_HEADER
    AC_DEFINE(HAVE_LIBGD)
  else
    AC_MSG_RESULT([- see term/gif.trm for download details])
  fi
fi
dnl end gd

dnl check for png and z library
if test "$with_png" != no; then
  gp_CHECK_LIB_PATH(png, png_info_init, -lz)
  if test "$ac_cv_lib_png_png_info_init" = yes; then
    gp_CHECK_HEADER(png.h,,
     AC_MSG_WARN([please add path to `echo $ac_safe|sed 's/\_/\./'` to INCLUDES in Makefile])
    )dnl gp_CHECK_HEADER
    gp_CHECK_HEADER(zlib.h,,
     AC_MSG_WARN([please add path to `echo $ac_safe|sed 's/\_/\./'` to INCLUDES in Makefile])
    )dnl gp_CHECK_HEADER
    AC_EGREP_CPP(yes,
     [#include "png.h"
     # if PNG_LIBPNG_VER < 89
       yes
     # endif
     ], AC_MSG_WARN([- not using old libpng version]), AC_DEFINE(HAVE_LIBPNG)
    )dnl AC_EGREP_CPP
  else
    AC_MSG_RESULT([- see term/png.trm for download details])
  fi
fi
dnl end png and z

dnl How do we detect the Ultrix X libraries?
dnl Or is it really a server (run-time) problem?
dnl FIXME AC_DEFINE(ULTRIX_KLUDGE)

dnl Cached variables
AC_CACHE_CHECK(CPPFLAGS,gp_cv_prog_make_cppflags,gp_cv_prog_make_cppflags="$CPPFLAGS")
AC_CACHE_CHECK(TERMLIBS,gp_cv_prog_make_termlibs,gp_cv_prog_make_termlibs="$TERMLIBS")

dnl Substitute variables
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(LASERGNU)
AC_SUBST(LINUXSUID)
AC_SUBST(TERMFLAGS)
AC_SUBST(TERMLIBS)


dnl rename makefiles in subdirectories, so that they don't take precedence
dnl over our created Makefiles

if test ! -f docs/makefile.dst && test -f docs/makefile && \
   test -z "$no_create"; then
  echo moving docs/makefile out of the way
  mv docs/makefile docs/makefile.dst
fi

if test ! -f docs/latextut/makefile.dst && \
   test -f docs/latextut/makefile && test -z "$no_create"; then
  echo moving docs/latextut/makefile out of the way
  mv docs/latextut/makefile docs/latextut/makefile.dst
fi

dnl Report configuration
AC_MSG_RESULT([
** Configuration summary for $PACKAGE $VERSION:

  Where is the help file?                    $datadir/gnuplot.gih
])
test "$ac_cv_header_gd_h" = yes && \
  AC_MSG_RESULT([  Enable generation of GIF files.])

test "$ac_cv_header_zlib_h" = yes && \
  AC_MSG_RESULT([  Enable generation of PNG files.])

test "$with_cwdrc" = yes && \
  AC_MSG_RESULT([  Check current directory for .gnuplot file.])

if test -f "$with_gnu_readline"; then
  AC_MSG_RESULT([  Use GNU readline library.])
else
  test "$with_readline" != no && \
    AC_MSG_RESULT([  Use included readline.])
fi

test "$with_lasergnu" = yes && \
  AC_MSG_RESULT([  Install lasergnu printer script.])

test "$with_linux_vga" = yes && \
  AC_MSG_RESULT([  Use the Linux console driver.])

test "$no_x" != yes && \
  AC_MSG_RESULT([  Use the X Window System.
])
dnl end config report

dnl Write Makefiles and configuration header
AC_OUTPUT([Makefile docs/Makefile docs/latextut/Makefile], \
[test -z "$CONFIG_HEADERS" || echo timestamp >stamp-h])

dnl end configure.in
