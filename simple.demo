#
# $Id: simple.demo,v 3.26 92/03/24 22:32:24 woo Exp Locker: woo $
#
#
# Requires data files "[123].dat" from this directory,
# so change current working directory to this directory before running.
# gnuplot> set term <term-type>
# gnuplot> load 'simple.demo'
#
set samples 50
plot [-10:10] sin(x),atan(x),cos(atan(x))
pause -1 "Hit return to continue"

set samples 100 
plot [-pi/2:pi] cos(x),-(sin(x) > sin(x+1) ? sin(x) : sin(x+1))
pause -1 "Hit return to continue"

set samples 200 
plot [-3:5] asin(x),acos(x)
pause -1 "Hit return to continue"

plot [-30:20] besj0(x)*0.12e1 with impulses, (x**besj0(x))-2.5 with points
pause -1 "Hit return to continue"

set samples 400
plot [-10:10] real(sin(x)**besj0(x))
pause -1 "Hit return to continue"

plot [-5*pi:5*pi] [-5:5] real(tan(x)/atan(x)), 1/x
pause -1 "Hit return to continue"

set autoscale
set samples 800
plot [-30:20] sin(x*20)*atan(x)
pause -1 "Hit return to continue"

plot [-19:19] '1.dat'with impulses ,'2.dat' ,'3.dat' with lines
pause -1 "Hit return to continue"

# undo what we have done above
set samples 160
set xrange [-10:10]
set autoscale xy
