#
# $Id: polar.demo,v 3.26 92/03/24 22:32:23 woo Exp Locker: woo $
#
#
# Show some of the new polar capabilities.
#
set polar
set dummy x
set samples 160
set xrange [0:2*pi]
set title "Three circles (with aspect ratio distortion)"
plot .5,1,1.5
pause -1 "Hit return to continue"
set title ""

plot cos(2*x)
pause -1 "Hit return to continue"

plot 2*sqrt(cos(x)),-2*sqrt(cos(x))
pause -1 "Hit return to continue"

set offset pi/4,pi/4,0,0
plot sin(4*x),cos(4*x)
set offset 0,0,0,0
pause -1 "Hit return to continue"

set yrange [-5:5]
plot x/cos(3*x)
pause -1 "Hit return to continue"
set autoscale

plot 1-sin(x)
pause -1 "Hit return to continue"

set xrange [0:12*pi]
plot 2*x
pause -1 "Hit return to continue"

butterfly(x)=exp(cos(x))-2*cos(4*x)+sin(x/12)**5
set samples 800
pause 0 "This is a big one (many samples), be patient..."
set title "Butterfly"
set nokey
plot butterfly(x)
pause -1 "Hit return to continue"

# undo what we've done above
set nopolar
set samples 160
set autoscale xy
set title ""
set offset 0,0,0,0
set key
