## a little demonstration of the 'boxes fill' plot style

reset

set samples 25

set style fill solid 1.0

set title "A demonstration of filled boxes with default widths"
plot [-10:10] 100/(1.0+x*x) title 'distribution' with boxes fill

pause -1 "Now draw the boxes with a black border"

set title "Filled boxes with the bsolid style"
set style fill solid border -1
replot

pause -1 "Now make the boxes a little less wide (-> return)"

set title "Filled boxes of reduced width"
set boxwidth 0.8 relative
replot

pause -1 "And now let's try a different fill density (-> return)"

set title "Filled boxes at 50% fill density"
set style fill solid 0.5 noborder
replot

pause -1 "Or maybe a hatch pattern, instead? (-> return)"

set title "Boxes filled with a hatch pattern"
set style fill pattern 1 border
replot

pause -1 "Finished this demo. (-> return)"
