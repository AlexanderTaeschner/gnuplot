#
# Use watchpoints to place labels on contour lines, but only
# at positions constrained to lie along a line
#
unset key
set xrange [-1 : 4]
set yrange [-3 : 3]
set zrange [-10 : 10]
f(x,y) = x**2 + y**2 * (1 - x)**3
set samples 51
set isosamples 51

set contours base
set cntrparam levels incr -9,3, 9
set view map
set title "Automatic label placement (3D)"
splot f(x,y) with lines nosurface, f(x,y) with labels
pause -1

set table $CONTOURS
splot f(x,y) with lines nosurface
unset table

#
# 2D version with constrained label placement
set margin 6,6,3,3
set title "Constrained label placement (2D)"

set style textbox  opaque margins  1.0, 2.0 fc  bgnd noborder
set style watch labels nopoint center offset -0.5,0 font ",10" 

line1(x,y) = y - (1.1*x -1)
line2(x,y) = y + x - 0.9

plot $CONTOURS with lines lw 2 watch y=0 label sprintf("%2d", int(z)), \
     '' with lines lt nodraw watch line1(x,y)=0 label sprintf("%2d ", int(z)), \
     '' with lines lt nodraw watch line2(x,y)=0 label sprintf("%2d ", int(z))

pause -1
reset
