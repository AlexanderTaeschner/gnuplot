# Illustrate the difference, if any, between plot styles
# "with filledcurves closed" and "with polygons".

$VERTICES << EOD
 -5.9608  0.9392
  1.7326  1.0500
  4.7459  0.5927
  3.6880  0.2498
  7.3104 -0.2681
  2.4378 -0.4646
 -0.4792 -0.1360
 -3.9092 -0.0109
 -6.7623  0.2676
 -4.8709  0.5070

 -1.9730  1.7596
  3.2406  1.8143
  4.9785  1.6039
  3.9915  1.2840
  6.2444  0.9399
  4.5279  0.7578
  2.6827  0.8862
 -0.9003  0.9494
 -2.5738  1.2051
EOD

set style fill transparent solid 0.2 border
unset key
unset tics
unset border
set tmargin 4

set multiplot layout 1,2 title "Compare closure of perimeter"

set title "with filledcurves" right
plot $VERTICES using 1:2:(column(-1)+3) with filledcurves lc var lw 4, \
            '' using 1:2:0 with labels offset .5,.5

set title "with polygons" right
plot $VERTICES using 1:2:(column(-1)+3) with polygons lc var lw 4, \
            '' using 1:2:0 with labels offset .5,.5

unset multiplot
pause -1 "<cr> to continue"
reset
