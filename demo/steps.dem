# Illustrate three variants of step plots - same data, three styles
#    plot with steps 
#    plot with fsteps 
#    plot with histeps 
#    plot with fillsteps 

set title "Compare steps, fsteps and histeps"
set key left

plot [0:12][0:13] "steps.dat" notitle with points ps 2 pt 6,  \
 "steps.dat" title 'steps' with steps, \
 'steps.dat' title 'fsteps' with fsteps, \
 'steps.dat' title 'histeps' with histeps

pause -1 "Hit return for demonstration of automatic histogram creation"
set title "Histogram built from unsorted data by 'smooth frequency'"
set ylabel 'counts per bin'
set xlabel 'bins'
set key right
plot 'hemisphr.dat' u (floor($1*20)):(1) smooth frequency with histeps title "with histeps"

pause -1 "Hit return to see the same plot with fillsteps"
set style fill solid 1.0
plot 'hemisphr.dat' u (floor($1*20)):(1) smooth freq with fillsteps title "with fillsteps"

pause -1 "Press return to continue"
reset
