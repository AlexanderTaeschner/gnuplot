# binary_polygon.dem
#
# Illustrate use of general keyword option "blank=NaN" to treat
# NaN records in a binary data file equiavalently to blank lines
# in a text input file.
#
unset border
unset tics
unset key

set xrange [-1:1]
set isotropic
set view 150, 55, 1.25
set pm3d lighting primary 0.5 specular 0.2 spec2 0.3
set pm3d border lc "black"

set multiplot layout 1,2 title "Compare text and binary input data"

set title "text input data\nusing 1:2:3:4 with polygons lc variable"
splot 'dodecahedron.dat' using 1:2:3:(column(-1)+3) with polygons lc variable

set title "binary format='%3float64%int32' blank=NaN\n using 1:2:3:4 with polygons lc variable"
splot 'dodecahedron.bin' binary format="%3float64%int32" blank=NaN \
      using 1:2:3:($4+3) with polygons lc variable

unset multiplot

pause -1 "<cr> to continue"

reset
