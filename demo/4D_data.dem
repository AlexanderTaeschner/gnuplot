#
# Demonstrate use of 4th data column to color a 3D surface
# Also demonstrates use of the pseudodata special file '+'
#
set view 49, 28, 1, 1.48
set xrange [ -15 : 15 ] noreverse nowriteback
set yrange [ -15 : 15 ] noreverse nowriteback
set zrange [ -0.25 : 1.0 ] noreverse nowriteback
set ticslevel 0
set samples 25, 25
set isosamples 50, 50

set title "4D data\nIndependent value color-mapped onto 3D surface" 
set xlabel "x" offset 3, 0, 0 
set ylabel "y" offset -5, 0, 0
set zlabel "z" offset 2, 0, 0 
set pm3d implicit at s

Z(x,y) = sin(sqrt(x**2+y**2))/sqrt(x**2+y**2)
color(x,y) = sin(x/5.)*cos(y/10.)

splot '+' using 1:2:(Z($1,$2)):(color($1,$2)) with pm3d title "4 data columns x/y/z/color"

pause -1 "Hit return to continue"

reset
